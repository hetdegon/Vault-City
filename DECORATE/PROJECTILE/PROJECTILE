ACTOR MSL_Player {
	Speed 1
	Damage 1
	DamageType "Pl_Impact"
	var int user_result;
	var int user_speed;
	var int user_damage;
	STATES {
		INIT:
			TNT1 A 0 //Set speed and damage from player.
	}
}


ACTOR MSL_HolyWater {
	Speed 32
	Radius 6
	Height 6
	Mass 20
	Damage 0
	DamageType "Pl_Purify"
	+NOBLOCKMAP
	+DROPOFF
	+MISSILE
	+EXPLODEONWATER
	+GRENADETRAIL
	BounceType "Grenade"
	BounceCount 1
	DeathSound "imp/shotx"
	Translation "0:255=%[0.00,0.00,0.00]:[1.00,1.00,1.00]"
	STATES{
		SPAWN:
			HLWT A 4 LIGHT("White64B")
			LOOP
		DEATH:
			TNT1 A 0 A_SpawnItemEx("FX_CylinderBurst",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
			TNT1 A 70 LIGHT("White128C") A_Explode(20, 256, false, true, 256)
			STOP
	}
}

ACTOR MSL_HEGrenade {
	Speed 32
	Radius 6
	Height 6
	Mass 20
	Damage 0
	+NOBLOCKMAP
	+DROPOFF
	+MISSILE
	+EXPLODEONWATER
	+GRENADETRAIL
	BounceType "Grenade"
	BounceCount 1
	DeathSound "imp/shotx"
	Translation "0:255=%[0.00,0.00,0.00]:[1.00,1.00,1.00]"
	STATES{
		SPAWN:
			HLWT A 4 LIGHT("White64B")
			LOOP
		DEATH:
			TNT1 A 0 A_SpawnItemEx("FX_Explode",random(-32, 32),random(-32, 32),random(1,32),0,0,frandom(0.4, 2.0),0,SXF_NOCHECKPOSITION)
			TNT1 A 0 LIGHT("Red64A") A_SetTics(random(1, 5))
			TNT1 A 1 A_FadeOut(0.1)
			TNT1 A 0 A_PlaySound("imp/shotx")
			TNT1 A 0 A_Explode(32, 160, false, true, 150)
			LOOP
	}
}

ACTOR MSL_StunGrenade {
	Speed 32
	Radius 6
	Height 6
	Mass 20
	Damage 0
	+NOBLOCKMAP
	+DROPOFF
	+MISSILE
	+EXPLODEONWATER
	+GRENADETRAIL
	+FORCEPAIN
	DamageType "Pl_Stun"
	BounceType "Grenade"
	BounceCount 2
	DeathSound "imp/shotx"
	Translation "0:255=%[0.00,0.00,0.00]:[1.00,1.00,1.00]"
	STATES{
		SPAWN:
			HLWT A 4 LIGHT("White64B")
			LOOP
		DEATH:
			TNT1 A 0 A_PlaySound("imp/shotx")
			TNT1 A 0 A_Explode(2, 128, false, true, 128)
			STOP
	}
}




ACTOR FX_Explode {
	RenderStyle "Add"
	+CLIENTSIDEONLY
	+NOINTERACTION
	+NOGRAVITY
	STATES{
		SPAWN:
			EXPL ABCDEFGH 1
			STOP
	}
}


ACTOR MSL_Flare1 {
	Speed 48
	Radius 16
	Height 8
	Mass 20
	DamageType "None"
	DeathSound "STEAM"
	+NOBLOCKMAP
	+DROPOFF
	+MISSILE
	+EXPLODEONWATER
	+DONTFALL
	+NOTRIGGER
	+BRIGHT
	+NOGRAVITY
	+FORCEXYBILLBOARD
	RenderStyle ADD
	Alpha 1.0
	var int user_loops;
	Translation "0:255=%[0.65,0.48,0.28]:[1.83,0.99,0.20]"
	STATES{
		SPAWN:
			TNT1 A 0 NODELAY A_SetUserVar("user_loops", 100)
			FLR1 BCBCBC 2 LIGHT("Yellow64CF")
			TNT1 A 0 A_ScaleVelocity(0.7)
			FLR1 BCBC 2 LIGHT("Yellow64CF")
			TNT1 A 0 A_ScaleVelocity(0.6)
			FLR1 BCBC 2 LIGHT("Yellow64CF")
			TNT1 A 0 A_ScaleVelocity(0.6)
			FLR1 BCBC 2 LIGHT("Yellow64CF")
			TNT1 A 0 A_ScaleVelocity(0.5)
			FLR1 BCBC 2 LIGHT("Yellow64CF")
			TNT1 A 0 A_ScaleVelocity(0.0)
			GOTO DEATH
		DEATH:
			TNT1 A 0 A_NoBlocking
			TNT1 A 0 A_JumpIf(user_loops <= 0, "DEATH2")
			TNT1 A 0 A_SetUserVar("user_loops", user_loops - 1)
			FLR1 BCDEDEDEDEDEDEDE 3 LIGHT("HiFlare") A_SpawnItemEx("FX_FlareYellow1", 0, 0, 10)
			LOOP

		DEATH2:
			TNT1 A 0 A_Scream
			STOP

	}

}

ACTOR MSL_FreezeGrenade {
	Speed 16
	Radius 8
	Height 8
	Mass 20
	Damage 0
	DamageType "Ice"
	+NOBLOCKMAP
	+DROPOFF
	+MISSILE
	+EXPLODEONWATER
	+GRENADETRAIL
	BounceType "Grenade"
	BounceFactor 0.6
	BounceCount 2
	Translation "0:255=%[0.00,0.00,0.00]:[0.00,0.00,2.00]"
	STATES{
		SPAWN:
			PLSS AB 4 BRIGHT LIGHT("SOULSPHERE")
			LOOP
		DEATH:
			BAL1 A 4 BRIGHT LIGHT("SOULSPHERE") A_Explode(64, 128, false, true, 64)
			BAL1 B 2 BRIGHT LIGHT("PLASMABALL") A_PlaySound("Weapon/GenericExplode", CHAN_WEAPON)
			BAL1 B 2 BRIGHT LIGHT("PLASMABALL") A_SetScale(6.0)
			BAL1 CDE 4 BRIGHT LIGHT("PLASMABALL")
			STOP

	}

}


ACTOR MSL_Spiker0 {
	Speed 48
	Radius 4
	Height 1
	Mass 1024
	DeathSound "STEAM"
	PROJECTILE
	Damage 192
	DamageType "Pl_Fire"
	+RIPPER
	+DONTREFLECT
	+PIERCEARMOR
	+NOFORWARDFALL
	+FORCEPAIN
	+EXPLODEONWATER
	+FLOORCLIP
	+BLOODLESSIMPACT
	var int user_initangle;
	var int user_initpitch;
	STATES{
		SPAWN:
			TNT1 A 0 NODELAY A_SetUserVar(user_initpitch, sin((velz/48)*90.0) * 90) //Seems sin((velz/maxspeed)*90) * 90 gives accurate enough results
			TNT1 A 0 A_SetUserVar(user_initangle, angle)
			SPKR A 2 BRIGHT LIGHT("White64C")
			SPKR A -1 BRIGHT LIGHT("Red32C")
			LOOP
		DEATH:
			TNT1 A 0 A_SetPitch(user_initpitch)
			SPKR A 3 BRIGHT LIGHT("White32C") A_CustomMissile("FX_RingBurst", 0.0, 0, user_initangle+180, CMF_AIMDIRECTION|CMF_ABSOLUTEANGLE|CMF_SAVEPITCH|CMF_ABSOLUTEPITCH, -user_initpitch)
			SPKR A 210 BRIGHT LIGHT("Red32C") A_NoBlocking
			SPKR A 350
			STOP

	}

}
